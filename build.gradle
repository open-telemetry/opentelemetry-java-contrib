plugins {
    id 'com.diffplug.spotless' version '5.1.1'
    id "com.github.johnrengelman.shadow" version "6.0.0" apply false
    id "com.jfrog.artifactory" version "4.17.2" apply false
    id 'com.jfrog.bintray' version '1.8.5' apply false
}

description = 'OpenTelemetry Contrib libraries and utilities for the JVM'

allprojects {
    group = 'io.opentelemetry.contrib'
    version = '0.0.1-SNAPSHOT'

    apply from: "$rootDir/gradle/spotless.gradle"
    apply from: "$rootDir/gradle/dependencies.gradle"

    it.ext.contrib = "$rootDir/gradle/contrib.gradle"
    it.ext.publish = "$rootDir/gradle/publish.gradle"

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
}

task lint(dependsOn: ['spotlessCheck'])
task format(dependsOn: ['spotlessApply'])

task integrationTest {
    doFirst {
        allprojects.test.each { test ->
            test.configure {
                systemProperty 'ojc.integration.tests', 'true'
            }
        }
    }
}
integrationTest.finalizedBy allprojects.test
