plugins {
    id 'com.diffplug.spotless' version '5.1.1'
    id "com.github.johnrengelman.shadow" version "6.0.0" apply false
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

description = 'OpenTelemetry Contrib libraries and utilities for the JVM'

allprojects {
    group = 'io.opentelemetry.contrib'
    version = '1.0.0-alpha'

    apply from: "$rootDir/gradle/spotless.gradle"
    apply from: "$rootDir/gradle/dependencies.gradle"

    it.ext.contrib = "$rootDir/gradle/contrib.gradle"
    it.ext.publish = "$rootDir/gradle/publish.gradle"

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
}

task lint(dependsOn: [allprojects.spotlessCheck])
task format(dependsOn: [allprojects.spotlessApply])

task integrationTest {
    doFirst {
        allprojects.test.each { test ->
            test.configure {
                systemProperty 'ojc.integration.tests', 'true'
            }
        }
    }
}
integrationTest.finalizedBy allprojects.test

// At this time authentication relies on sonatypeUsername and sonatypePassword project properties or
// ORG_GRADLE_PROJECT_sonatypeUsername and ORG_GRADLE_PROJECT_sonatypePassword environment variables.
nexusPublishing {
    repositories {
        sonatype()
    }
}
